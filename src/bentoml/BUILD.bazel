load("@rules_python//python:defs.bzl", "py_library")
load("//rules:python.bzl", _FRAMEWORK_SRCS = "SUPPORTED_FRAMEWORKS_TARGETS")
load("@pypi//:requirements.bzl", "requirement")

package(default_visibility = [
    "//:__subpackages__",
    "//tests:__subpackages__",
])

# TODO: "detectron", "easyocr", "evalml", "fasttext", "flax", "gluon", "h2o", "onnxmlir", "paddle", "pycaret", "pyspark", "spacy", "statsmodels", "tensorflow_v1",

[
    py_library(
        name = src,
        srcs = [
            "{}.py".format(src),
            "//src/bentoml/_internal:srcs_files",
        ],
        data = ["//src/bentoml/_internal:data_files"],
        deps = [
            "//src/bentoml",
            "//src/bentoml/_internal/frameworks:{}".format(src),
        ],
    )
    for src in _FRAMEWORK_SRCS
]

py_library(
    name = "bentoml",
    srcs = [
        "//src/bentoml/_internal:srcs_files",
        "//src/bentoml/grpc:srcs_files",
        "//src/bentoml/testing:srcs_files",
    ] + glob(["*.py"]),
    data = [
        "//src/bentoml/_internal:data_files",
        "//src/bentoml/_internal:srcs_files",
        "//src/bentoml/grpc:srcs_files",
    ],
    imports = ["../"],
    deps = select({
        "@bazel_tools//src/conditions:linux": [
            requirement("grpcio"),
            requirement("protobuf"),
            requirement("pandas"),
            requirement("pydantic"),
            requirement("simple-di"),
            requirement("Pillow"),
            requirement("pip-requirements-parser"),
        ],
        "@bazel_tools//src/conditions:darwin": [
            "//:grpcio",
            "//:protobuf",
            requirement("pandas"),
            requirement("pydantic"),
            requirement("simple-di"),
            requirement("Pillow"),
            requirement("pip-requirements-parser"),
        ],
    }),
)

filegroup(
    name = "srcs_files",
    srcs = [
        "//:README.md",  # This is used for metadata for wheels.
        "//src/bentoml/_internal:data_files",
        "//src/bentoml/_internal:srcs_files",
        "//src/bentoml/grpc:srcs_files",
        "//src/bentoml/testing:srcs_files",
    ],
)
